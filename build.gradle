apply plugin: 'com.android.library'

android {
  compileSdkVersion 27

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  testImplementation 'junit:junit:4.12'
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support:cardview-v7:27.1.1'
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
  }
}

group = 'com.tbuonomo.andrui'
version = '1.0.2'

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

task generateSourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier 'sources'
}

artifacts {
  archives generateSourcesJar
}

bintray {
  Properties localProps = new Properties()
  localProps.load(new FileInputStream(file('local.properties')))

  user = localProps.getProperty("bintrayUser")
  key = localProps.getProperty("bintrayApiKey")
  pkg {
    repo = 'maven'
    name = 'material-square-loading'

    version {
      name = version
      desc = 'A cool squares loading animation in Android'
      vcsTag = version
    }

    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/tommybuonomo/materialsquareloading'
    websiteUrl = 'https://github.com/tommybuonomo/materialsquareloading'
  }
  configurations = ['archives']
}
